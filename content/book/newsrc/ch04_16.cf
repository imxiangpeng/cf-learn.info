body package_method apt
{
     package_changes => "bulk";
     package_list_command => "/usr/bin/dpkg -l"; # <1>
     package_list_name_regex    => "ii\s+([^\s]+).*"; # <2>
     package_list_version_regex => "ii\s+[^\s]+\s+([^\s]+).*";
     package_installed_regex => ".*"; # all reported are installed
     package_name_convention => "$(name)"; # <3>

   # set it to "0" to avoid caching of list during upgrade
     package_list_update_ifelapsed => "240";

  have_aptitude:: # <4>
     package_add_command => "/usr/bin/aptitude --assume-yes install";
     package_list_update_command => "/usr/bin/aptitude update";
     package_delete_command => "/usr/bin/aptitude --assume-yes -q remove";
     package_update_command => "/usr/bin/aptitude --assume-yes install";
     package_verify_command => "/usr/bin/aptitude show";
     package_noverify_regex =>
       "(State: not installed|E: Unable to locate package .*)";

  !have_aptitude::
     package_add_command => "/usr/bin/apt-get --yes install";
     package_list_update_command => "/usr/bin/apt-get update";
     package_delete_command => "/usr/bin/apt-get --yes -q remove";
     package_update_command =>  "/usr/bin/apt-get --yes install";
     package_verify_command => "/usr/bin/dpkg -s";
     package_noverify_returncode => "1";
}
