body agent control
{
        files_single_copy => { ".*" }; # <1>
}

bundle agent copyfiles
{
  vars:
      # Suffixes to try, in most-specific to most-general order. This must
      # include the empty suffix at the end, for the most general file.
      "suffixes"
        slist => { ".$(sys.fqhost)", ".$(sys.uqhost)", ".$(sys.domain)", # <2>
                   ".$(sys.flavor)", ".$(sys.ostype)", "" };
      # List of files to copy
      "filestocopy"     slist => { "/etc/hosts", "/etc/motd" };   # <3>
      "dirstocopy"      slist => { "$(sys.workdir)/bin", "/usr/local/bin" };
      # Source of the files
      "repo"            string => "/mnt/fileserver/cfengine/files";   # <4>
      # Destination for the files
      # Set this to an empty string for a production environment
      # "dest" string => "";
      "dest"            string => "/tmp/testdest";   # <5>

  files:
      "$(dest)$(filestocopy)"   # <6>
        copy_from => local_dcp("$(repo)$(filestocopy)$(suffixes)");

      "$(dest)$(dirstocopy)"    # <7>
        copy_from => local_dcp("$(repo)$(dirstocopy)$(suffixes)"),
        depth_search => recurse("inf");
}
